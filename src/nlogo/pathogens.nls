;; Functions concerned with plant pathogens
;; rust - long distance random spread
;; PTA - llocal and long distance

to spread-rust
  
  ;; random binomial trial on new infections
  let susc patches with [(table:get rust-susc-dict class) = true and myrtle-rust? = false]
 
  ask n-of ((random-binomial (count susc) rust-global-inf)) susc
  [
    set myrtle-rust? true
    set myrtle-rust-time ticks
  ]

end  


to spread-kauri-mate
  
  ;; global infection -  random binomial trial on new infections
  let susc patches with [(table:get phy-susc-dict class) = true and myrtle-rust? = false]
  set susc susc with [ prev-class != "yf-nok" ] ; should filter out old forest that came on non-k traj
  
  ask n-of ((random-binomial (count susc) phy-global-inf)) susc
  [
    set kauri-mate? true
    set kauri-mate-time ticks
  ]
  
  ;; local spread, exlc patches globally infected this tick   (TO DO TRANSMISSION TIME)
  ask patches with [kauri-mate? = true and kauri-mate-time != ticks]
  [
    ask kauri-mate-nhb with [kauri-mate? = false]
    [
      if random-float 1 <= phy-local-inf 
      [
        set kauri-mate? true
        set kauri-mate-time ticks 
      ]  
    ]
  ]  
  
end

    
;; for patches with kauri mate does loss of kauri or retrogression occur?  
to kauri-mate-switch
  
  ;; critical time is the time with PTA before any risk of retrogression
  let crit-time table:get aux-params-dict "kaurimate-retro-age"
  
  ;; look for infected patches that have been infected long enough
  ask patches with [ kauri-mate? = true and kauri-mate-time > (ticks + crit-time) ]
  [
    if class = "yf-k" 
    [ 
      let base table:get base-changes-dict "yf-k"
      let pc incr-limit-fx ((ticks + crit-time) / base) 0.05 1.0
  
      if random-float 1 <= pc  ;; rand number test based on asym incr func
      [    
          set class "yf-nok" 
      ]
    ]
    
    if class = "ksh-k"       ;; ksh goes to either alt state based on age
    [
      let base table:get base-changes-dict "ksh-k"
      let pc incr-limit-fx ((ticks + crit-time) /  base) 0.05 1.0
        
      if random-float 1 <= pc  ;; rand number test based on asym incr func
      [
        ifelse last-change < (ticks + base / 2)  ;  1/2 base change
        [
          set class "ksh-nok"
        ]
        [
          set class "m-sh"  
        ]
      ]
    ]   
  ]
end  
