;; -----------------------------------------
;; MAIN LOOP setup
;; -----------------------------------------


to setup
  clear-all
  if from-r? != TRUE [set from-r? FALSE]

  set world-size count patches
  set patch-grain 20
  set n-states 11

  set record-tag ""

  set rnd-seed new-seed

  set colour-dict table:from-list [
    ["gr" 19]
    ["d-sh" 38]
    ["m-sh" 34]
    ["ksh-k" 27]
    ["yf-k" 24]
    ["ksh-nok" 104]
    ["yf-nok" 107]
    ["old-f" 63]
    ["ksh-p" 88]
    ["yf-p" 85]
    ["old-p" 83]
  ]
  
  set class-names-list (list "gr" "d-sh" "m-sh" "ksh-k" "yf-k" "ksh-nok" "yf-nok" "old-f" "ksh-p" "yf-p" "old-p")
  set forest-classes-list (list "yf-k" "yf-nok" "old-f" "yf-p" "old-p")

  set flammability-dict table:from-list [
    ["gr" 0.5]
    ["d-sh" 0.65]
    ["m-sh" 0.35]
    ["ksh-k" 0.25]
    ["yf-k" 0.1]
    ["ksh-nok" 0.25]
    ["yf-nok" 0.1]
    ["old-f" 0.05]
    ["ksh-p" 0.25]
    ["yf-p" 0.1]
    ["old-p" 0.05]
  ]

  set base-changes-dict table:from-list [
    ["gr" 20]
    ["d-sh" 20]
    ["m-sh" 30]
    ["ksh-k" 80]
    ["yf-k" 120]
    ["ksh-nok" 80]
    ["yf-nok" 120]
    ["old-f" 0]
    ["ksh-p" 80]
    ["yf-p" 120]
    ["old-p" 0]
  ]

  ;; Set up the transition matrices for seedling/sapling dynamics -> ROW wise
  set tr-mtx-bank-yfor (list [0.8 0] [0.1 0.9])    ;; transitions from 1.5m -> 5 cm dbh individuals in yng forest
  set tr-mtx-bank-ofor (list [0.8 0] [0.1 0.9])    ;; ... and old forest

  ;; Set up fire history 
  set fire-record []
  set fire-stats []
  set fire-size-list []
  set beyond-flamm-time 99999

  ;; Setup ENSO
  set enso-record []
  init-enso
 
  ;; Build the landscape using the MRC code...
  build-landscape

  reset-ticks
end

;; drive the MRC landscape building
to build-landscape
  set clusters []
  set class-list n-values n-states [ i -> i ]

  set sum-stalled (list 0 0)
  set sum-changes (list 0 0)

  ;; set up the MRC landscape
  init-patch-mrc-variables
  read-class-file-csv init-composition
  setup-mrc true false ;; args:: remove-singleton-patches? sequentially-assign-clusters?
  
  ask patches [set class item class class-names-list]
  
  ;; Ensure that there are no cells in the 'invaded' state if invasion is false
  if invasion? = false
  [
    ask patches with [class = "d-sh"] [set class "m-sh"]
  ]

  init-patch-dynamics
  find-clusters
  id-clusters
  age-by-cluster

  update-abundances


  ask patches [set distance-to-coast pxcor * patch-grain]

end

;; initialise the patch fire and regen history
to init-patch-dynamics

  ask patches
  [
    set times-change 0
    set flammability table:get flammability-dict class
    set burned? false
    set regenbank-yfor (list 0 0)
    set regenbank-ofor (list 0 0)

    set fire-history []
    set next-change table:get base-changes-dict class
    set t-colonised (list -1 -1)
    set prev-class class
  ]

  color-patches
end
