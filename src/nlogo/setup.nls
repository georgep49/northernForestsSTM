;; -----------------------------------------
;; MAIN LOOP setup
;; -----------------------------------------

to setup
  clear-all
  
  ;;sr:setup
  
  show "Building parameter dictionaries..."
  
  ; if from-r? != TRUE [set from-r? FALSE]

  set world-size count patches
  set patch-grain 20
  set n-states 11

  set record-tag ""

  set rnd-seed new-seed

  set colour-dict table:from-list [
    ["gr" 19]
    ["d-sh" 38]
    ["m-sh" 34]
    ["ksh-k" 27]
    ["yf-k" 24]
    ["ksh-nok" 107]
    ["yf-nok" 104]
    ["old-f" 63]
    ["ksh-p" 88]
    ["yf-p" 85]
    ["old-p" 83]
  ]
  
  set class-list n-values n-states [ i -> i ]
  set class-names-list (list "gr" "d-sh" "m-sh" "ksh-k" "yf-k" "ksh-nok" "yf-nok" "old-f" "ksh-p" "yf-p" "old-p")
  set forest-classes-list (list "yf-k" "yf-nok" "old-f" "yf-p" "old-p")

  ;; load dictionaries (NL tables) from csv files
  set flammability-dict table:from-list csv:from-file "parameter_files/flammability_table.csv"
  set base-changes-dict table:from-list csv:from-file "parameter_files/base_changes_table.csv"
  set rust-susc-dict table:from-list csv:from-file "parameter_files/rust_table.csv"
  set phy-susc-dict table:from-list csv:from-file "parameter_files/kaurimate_table.csv"
  
  ;; Load other parameter files
  load-aux-parameters
  
  ;; Set up the transition matrices for seedling/sapling dynamics -> ROW wise
  set tr-mtx-bank-yfor (list [0.8 0] [0.05 0.9])    ;; transitions from 1.5m -> 5 cm dbh individuals in yng forest
  set tr-mtx-bank-ofor (list [0.8 0] [0.05 0.9])    ;; ... and old forest
  
  ;; Set up fire history 
  set fire-record []
  set fire-stats []
  set fire-size-list []
  set fire-year []
  set beyond-flamm-time 99999

  ;; Setup clamate, ENSO, ...
  show "Setting up ENSO dynamics..."
  set enso-record []
  init-enso
  
  ;; Build the landscape using the MRC code...
  show "Building starting landscape..."
  build-landscape
  
  reset-ticks
end


to load-aux-parameters
  
  ;; general parameters for curves etc.
  set aux-params-dict csv:from-file "parameter_files/aux_parameters.csv" 
  set aux-params-dict table:from-list aux-params-dict
  
  ;; fire wind and slope modifiers
  ;; we strip off the title so it is a list of numeric lists
  let X map but-first (csv:from-file "parameter_files/fire_weights.csv")
  
  set flamm-wind-list but-last X
  set flamm-slope-list last X
    
end

;; drive the MRC landscape building
to build-landscape
  set clusters []
  

  set sum-stalled (list 0 0)
  set sum-changes (list 0 0)

  
  ;; set up the MRC landscape
  show "Building starting landscape (MRC)..."
  init-patch-mrc-variables
  setup-mrc true false ;; args:: remove-singleton-patches? sequentially-assign-clusters?
  
  ask patches [set class item class class-names-list]
  
  ;; Ensure that there are no cells in the 'invaded' state if invasion is false
  if invasion? = false
  [
    ask patches with [class = "d-sh"] [set class "m-sh"]
  ]
  
  show "Building starting landscape (cluster id)..."
  init-patches
  find-clusters
  id-clusters
  age-by-cluster
  
  show "Building starting landscape (coastal & farmland)..."
  starting-coastal-forest
  if farm-edge? [edge-farmland]
  
  update-abundances
  color-by-class
  
end

;; initialise the patch fire and regen history
to init-patches

  ask patches
  [
    set times-change 0
    set flammability table:get flammability-dict class
    set burned? false
    set regenbank-yfor (list 0 0)
    set regenbank-ofor (list 0 0)
    
    set fire-history []
    set next-change table:get base-changes-dict class
    set t-colonised (list -1 -1)
    set prev-class class
    
    set distance-to-coast pxcor * patch-grain
    set farm-node? false
    
    set myrtle-rust? false
    set kauri-mate? false
    set kauri-mate-nhb other patches in-radius phy-radius-inf
  ]
  
  ;;build-fbm
  load-fbm
end
